/*
from https://www.w3.org/Protocols/rfc822/
*/

// "examples:test1@example.com,test2@example.com;"
emailaddress = { SOI ~ ( group | mailbox ) ~ EOI }

group = { displayname ~ ":" ~ mailboxes* ~ ";" }

mailboxes = { (mailbox | ",") }

displayname = { phrase }

mailbox =  { addrspec | ( phrase ~ routeaddr ) }

// hans<@oke,@paard:peter@example.com>
// examples:hans<hans@example.com>,peter<peter@example.com>;
// examples:"Hans Example"<hans@example.com>,peter<peter@example.com>;

routeaddr = {  "<" ~ route* ~ addrspec ~ ">" }

route = { ( ("@" ~ domain) | ",")+ ~ ":" }

addrspec = _{ localpart ~ "@" ~ domain }

localpart = { word ~ ("." ~ word)* }

domain = { subdomain ~ ("." ~ subdomain)* }

subdomain = _{ domainliteral | domainref}

domainref = _{ atom }

CHAR = _{ '\u{0000}'..'\u{007F}' }

ALPHA = _{ '\u{0041}'..'\u{005A}' | '\u{0061}'..'\u{007A}' }

DIGIT = _{ '\u{0030}'..'\u{0039}' }

CTL = _{ '\u{0000}'..'\u{001F}' | "\u{007F}"}

CR = _{"\u{000D}"}

LF =  _{"\u{000A}"}

SPACE = _{"\u{0020}"}

HTAB = _{"\u{0009}"}

QUOTE = _{"\u{0022}"}

CRLF = _{ CR ~ LF }

LWSPCHAR = _{ SPACE | HTAB }

lwsp = _{ (CRLF? ~ LWSPCHAR)+ }

SPECIALS = _{ "(" | ")" | "<" | ">" | "@"  |  "," | ";" | ":" | "\\" | QUOTE |  "." | "[" | "]" }

delimeters = _{ SPECIALS | lwsp | comment }

text = _{ CHAR }

atom = _{ ( !(SPECIALS | SPACE | CTL ) ~ CHAR)+ }

quotedstring = _{ QUOTE ~ (QTEXT | quotedpair)* ~ QUOTE }

QTEXT =  _{ !( QUOTE | "\\" | CR ) ~ ( CHAR | lwsp )}

domainliteral = _{ "[" ~ (DTEXT | quotedpair)* ~ "]" }

DTEXT = _{ !( "[" | "]" | "\\" | CR ) ~ ( CHAR | lwsp )}

comment = { "(" ~ (CTEXT | quotedpair | comment)* ~ ")" }

CTEXT = _{ !( "(" | ")" | "\\" | CR ) ~ ( CHAR | lwsp )}

quotedpair = _{ "\\" ~ CHAR }

phrase = _{ word+ }

word = _{ quotedstring | atom }
